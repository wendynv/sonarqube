sonarqube {
  properties {
    property 'sonar.projectName', 'SonarQube :: Database Core'
  }
}

dependencies {
  compileOnly 'com.google.code.findbugs:jsr305'

  compile 'commons-codec:commons-codec'
  compile 'org.apache.commons:commons-csv'
  compile project(':sonar-scanner-protocol')
  compile project(':sonar-core')
  compile project(path: ':sonar-plugin-api', configuration: 'shadow')
  compile 'org.mybatis:mybatis'
  compile 'commons-dbcp:commons-dbcp'
  compile 'commons-dbutils:commons-dbutils'
  compile 'net.jpountz.lz4:lz4'

  compile 'org.slf4j:slf4j-api'
  compile 'ch.qos.logback:logback-classic'
  compile 'ch.qos.logback:logback-core'
  compile project(':server:sonar-process')

  testCompile 'junit:junit'
  testCompile 'org.assertj:assertj-core'
  testCompile 'org.mockito:mockito-core'
  testCompile 'org.dbunit:dbunit'
  testCompile project(':sonar-testing-harness')
  testCompile 'mysql:mysql-connector-java'
  testCompile 'org.postgresql:postgresql'
  testCompile 'com.h2database:h2'
  testCompile 'com.google.code.findbugs:jsr305'
  testCompile 'org.sonarsource.orchestrator:sonar-orchestrator'

  if (System.getProperty('orchestrator.configUrl')) {
    compile 'com.microsoft.sqlserver:mssql-jdbc'
    compile 'mysql:mysql-connector-java'
    compile 'org.postgresql:postgresql'
    compile 'com.oracle.jdbc:ojdbc8'
  }
}

task createDB(type:JavaExec) {
  main = 'org.sonar.db.CreateDb'
  classpath = sourceSets.test.runtimeClasspath
  systemProperty 'orchestrator.configUrl', System.getProperty('orchestrator.configUrl')
  if (project.hasProperty('cix')) {
    systemProperty 'sonar.runtimeVersion', version
  }
  systemProperty 'orchestrator.artifactory.apiKey', System.getProperty('orchestrator.artifactory.apiKey')
  systemProperty 'orchestrator.artifactory.repositories', System.getProperty('orchestrator.artifactory.repositories')
}

test {
  if (System.getProperty('orchestrator.configUrl'))
    systemProperty 'orchestrator.configUrl', System.getProperty('orchestrator.configUrl')
}

task testJar(type: Jar) {
  classifier = 'tests'
  from sourceSets.test.output
}

artifacts {
  archives testJar
}

artifactoryPublish.skip = false

// Used by core plugins
apply plugin: 'maven-publish'
publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
      artifact testJar
    }
  }
}
