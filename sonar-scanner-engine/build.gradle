sonarqube {
  properties {
    property 'sonar.projectName', 'SonarQube :: Scanner Engine'
  }
}

// TODO otherwise not considered as up-to-date - 'gradle -i' shows
//   file sonar-scanner-engine/src/test/resources/mediumtest/xoo/multi-modules-sample/.sonar/batch-report/metadata.pb has changed
//   file sonar-scanner-engine/src/test/resources/mediumtest/xoo/sample-generic-coverage/.sonar/batch-report/metadata.pb has changed
//   file sonar-scanner-engine/src/test/resources/mediumtest/xoo/sample-generic-test-exec/.sonar/batch-report/metadata.pb has changed
sourceSets.test.resources {
  exclude '**/.sonar/**'
}

configurations {
  testCompile.extendsFrom(compileOnly)
}

dependencies {
  compileOnly 'com.google.code.findbugs:jsr305'
  compile 'org.sonarsource:sonar-persistit'
  compile 'com.google.guava:guava'
  compile project(':sonar-core')
  compile project(':sonar-scanner-protocol')
  compile project(':sonar-ws')
  compile project(':sonar-home')
  compile project(path: ':sonar-plugin-api', configuration: 'shadow')
  compile 'org.slf4j:slf4j-api'
  compile 'org.slf4j:log4j-over-slf4j'
  compile 'org.slf4j:jul-to-slf4j'
  compile 'commons-lang:commons-lang'
  compile 'commons-codec:commons-codec'
  compile 'com.google.code.gson:gson'
  compile 'org.freemarker:freemarker'

  testCompile 'junit:junit'
  testCompile 'com.tngtech.java:junit-dataprovider'
  testCompile 'org.assertj:assertj-core'
  testCompile 'net.javacrumbs.json-unit:json-unit-assertj:0.0.15'
  testCompile 'org.mockito:mockito-core'
  testCompile project(':sonar-plugin-api').sourceSets.test.output
  // Workaround to resolve dependencies of eclipse specific packaging: https://issues.gradle.org/browse/GRADLE-2076
  testCompile module('org.eclipse.jetty:jetty-server') {
    dependencies(
      "org.eclipse.jetty.orbit:javax.servlet:3.0.0.v201112011016@jar",
      "org.eclipse.jetty:jetty-continuation:8.1.2.v20120308",
      "org.eclipse.jetty:jetty-http:8.1.2.v20120308"
    )
  }
  testCompile 'javax.servlet:javax.servlet-api'
  testCompile project(':plugins:sonar-xoo-plugin')
}

artifactoryPublish.skip = false

// Used by core plugins
apply plugin: 'maven-publish'
publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
    }
  }
}
